buildscript {
    repositories {
        mavenLocal()
        // If using a Maven proxy, put the property settings in
        // ~/.gradle/gradle.properties for default_proxy_url,
        // default_proxy_username and default_proxy_password.
        if (project.hasProperty("default_proxy_url")) {
            maven {
                url project.property("default_proxy_url")
                credentials {
                    username project.property("default_proxy_username")
                    password project.property("default_proxy_password")
                }
            }
        }
        jcenter()
        mavenCentral()
    }
}

plugins {
    id "java-library"
    id "groovy"
    id "maven-publish"
    id "com.jfrog.bintray" version "1.8.4"
}

version projectVersion
group projectGroup

sourceCompatibility = jdkCompatibilityVersion
targetCompatibility = jdkCompatibilityVersion

repositories {
    mavenLocal()
    // If using a Maven proxy, put the property settings in
    // ~/.gradle/gradle.properties for default_proxy_url,
    // default_proxy_username and default_proxy_password.
    if (project.hasProperty("default_proxy_url")) {
        maven {
            url project.property("default_proxy_url")
            credentials {
                username project.property("default_proxy_username")
                password project.property("default_proxy_password")
            }
        }
    }
    jcenter()
    mavenCentral()
}

jar {
    from { ["LICENSE", "NOTICE"] }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    from { ["LICENSE", "NOTICE"] }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    from { ["LICENSE", "NOTICE"] }
}

dependencies {
    /**
     * These dependencies are cobbled together using the top-level ApacheDS
     * source code pom.xml and service/pom.xml.  Want to stay away from the
     * monolithic JARs of packages like apacheds-all and apacheds-service,
     * which package a bunch of different libraries into one JAR, which does
     * a real good job of breaking dependency management (and Java9+
     * modularity) in apps that depend on apacheds-embedded.
     */
    def apacheDsVersion = "2.0.0.AM25"
    // This is org.apache.directory.api.version from top-level pom.xml.
    def apacheDirApiVersion = "2.0.0.AM1"
    // From service/pom.xml
    api group: "org.apache.directory.server", name: "apacheds-core-annotations", version: apacheDsVersion
    api group: "org.apache.directory.server", name: "apacheds-core-api", version: apacheDsVersion
    api group: "org.apache.directory.server", name: "apacheds-service-builder", version: apacheDsVersion
    api group: "org.apache.directory.api", name: "api-ldap-codec-standalone", version: apacheDirApiVersion
    api group: "org.apache.directory.api", name: "api-ldap-model", version: apacheDirApiVersion
    api group: "org.apache.directory.api", name: "api-ldap-schema-data", version: apacheDirApiVersion
    api group: "org.apache.directory.api", name: "api-util", version: apacheDirApiVersion

    implementation "commons-io:commons-io:2.6"

    testImplementation "org.codehaus.groovy:groovy-all:2.5.9"
    testImplementation "org.spockframework:spock-core:1.3-groovy-2.5"
    testImplementation "org.slf4j:slf4j-api:1.7.30"
    testRuntimeOnly "org.apache.logging.log4j:log4j-core:2.13.0"
    testRuntimeOnly "org.apache.logging.log4j:log4j-slf4j-impl:2.13.0"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar { classifier "sources" }
            artifact javadocJar { classifier "javadoc" }
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name projectName
                    description projectDescription
                    url githubWebUrl
                    scm {
                        url githubWebUrl
                        connection "scm:git:${githubWebUrl}.git"
                        developerConnection "scm:git:${githubWebUrl}.git"
                    }
                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/license/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }
                }
            }
        }
    }
}

if (project.hasProperty('default_bintray_user')) {
    // deploy with: gradle bintrayUpload
    bintray {
        // put in ~/.gradle/gradle.properties
        user = project.property('default_bintray_user')
        key = project.property('default_bintray_api_key')
        publications = ['mavenJava']

        //dryRun = true

        pkg {
            repo = 'maven'
            name = projectName
            desc = projectDescription
            licenses = ['Apache-2.0']
            websiteUrl = githubWebUrl
            vcsUrl = "${githubWebUrl}.git"
            issueTrackerUrl = "${githubWebUrl}/issues"
            githubRepo = githubRepo
            version {
                name = project.version
                desc = "${projectName} v${project.version} - ${projectDescription}"
                released = new Date()
                vcsTag = "v${project.version}"
            }
        }
    }
}
